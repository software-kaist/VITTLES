apply plugin: 'com.android.application'

android {
    compileSdkVersion 22
    buildToolsVersion "23.0.0 rc3"

    sourceSets {
        main {
            jni.srcDirs = []
        }
    }

    defaultConfig {
        applicationId "kaist.game.battlecar"
        minSdkVersion 19
        targetSdkVersion 22
	versionCode 1
        versionName "1.0"

        ndk {
            moduleName "CarViewStreamer"
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:22.2.1'
    compile 'com.android.support:support-v4:22.2.1'
    compile fileTree(dir: "$buildDir/libs", include: 'ndk-libs')
    // Adds our jar file containing NDK code (.so files)
    compile files("$buildDir/libs/ndk-libs.jar")
    // Just debug info
    println("$buildDir/libs")
}

// Before compiling our app, prepare NDK code
tasks.withType(JavaCompile) {
    compileTask -> compileTask.dependsOn ndkLibsToJar
}

// Packs ndk into JAR
task ndkLibsToJar(type: Zip, dependsOn: 'ndkBuild', description: 'Create a JAR of the native libs') {
    destinationDir new File(buildDir, 'libs')
    baseName 'ndk-libs'
    extension 'jar'
    from(new File(buildDir, 'libs')) { include '**/*.so' }
    into 'lib/'
}

// Build native code using mk files like on Eclipse
task ndkBuild(type: Exec, description: 'Compile JNI source via NDK') {
    //def ndkDir = project.plugins.findPlugin('com.android.application').getNdkFolder()
    //println(project.plugins.findPlugin('com.android.application').getNdkFolder())
    def ndkDir = plugins.getPlugin('com.android.application').sdkHandler.getNdkFolder()
    println(plugins.getPlugin('com.android.application').sdkHandler.getNdkFolder())

    commandLine "$ndkDir/ndk-build",
            'NDK_PROJECT_PATH=build',
            'APP_BUILD_SCRIPT=src/main/jni/Android.mk',
            'NDK_APPLICATION_MK=src/main/jni/Application.mk'


}

